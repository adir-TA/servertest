

function rng(min: integer, max: integer) :: integer:
    set {_int} to a random integer between {_min} and {_max}
    return {_int}

function team_getAllTeamsIDs() :: integers:
    return {data.teams::ids::*}

function team_getAllTeamsNames() :: strings:
    return {data.teams::names::*}

function team_getMessage(type: string, value: string="", value2: string="") :: strings:


    # ============ HELP MESSAGE ============

    if {_type} = "help":
        add "" to {_message::*}
        add "&6&lTEAM COMMANDS" to {_message::*}
        add "" to {_message::*}
        add "&e/team create <name> &8- &7Create a new team" to {_message::*}
        add "&e/team disband &8- &7Delete your team (owner)" to {_message::*}
        add "&e/team leave &8- &7Leave your current team" to {_message::*}
        add "&e/team info [player] &8- &7View team info" to {_message::*}
        add "&e/team invite <player> &8- &7Invite a player (mod+)" to {_message::*}
        add "&e/team join <team> &8- &7Join a team you're invited to" to {_message::*}
        add "&e/team kick <player> &8- &7Kick a member (mod+)" to {_message::*}
        add "&e/team promote <player> &8- &7Promote to mod (owner)" to {_message::*}
        add "&e/team demote <player> &8- &7Demote to member (owner)" to {_message::*}
        add "" to {_message::*}
        return {_message::*}

    # ============ SUCCESS MESSAGES ============

    if {_type} = "team_create":
        add "&2&lTEAM CREATED!" to {_message::*}
        add "&aYou created team &f%{_value}%&a!" to {_message::*}
        return {_message::*}

    if {_type} = "team_disband_confirm":
        add "&6&lCONFIRM DISBAND" to {_message::*}
        add "&eType '&fI confirm deletion of team %{_value}%&e'" to {_message::*}
        add "&eOr type anything else to cancel." to {_message::*}
        return {_message::*}

    if {_type} = "team_disband_message_to_members":
        add "&4&lTEAM DELETED" to {_message::*}
        add "&cTeam &f%{_value}%&c was deleted by the owner." to {_message::*}
        return {_message::*}

    if {_type} = "team_disband_message_to_owner":
        add "&6&lTEAM DISBANDED" to {_message::*}
        add "&eYou deleted team &f%{_value}%&e." to {_message::*}
        return {_message::*}

    if {_type} = "team_disband_cancelled":
        add "&6&lCANCELLED" to {_message::*}
        add "&eTeam disband cancelled." to {_message::*}
        return {_message::*}

    if {_type} = "kick_success_team":
        add "&6&lMEMBER KICKED" to {_message::*}
        add "&e%{_value}%&e was kicked by &f%{_value2}%&e." to {_message::*}
        return {_message::*}

    if {_type} = "kick_success_target":
        add "&4&lKICKED" to {_message::*}
        add "&cYou were kicked from &f%{_value}%&c." to {_message::*}
        return {_message::*}

    if {_type} = "invite_sent":
        add "&2&lINVITE SENT" to {_message::*}
        add "&aInvited &f%{_value}%&a to &f%{_value2}%&a. (60s)" to {_message::*}
        return {_message::*}

    if {_type} = "invite_received":
        add "&6&lTEAM INVITE" to {_message::*}
        add "&e%{_value2}%&e invited you to &f%{_value}%&e." to {_message::*}
        add "&eType &f/team join %{_value}%&e to accept. (60s)" to {_message::*}
        return {_message::*}

    if {_type} = "invite_expired":
        add "&4&lEXPIRED" to {_message::*}
        add "&cInvite to &f%{_value}%&c expired." to {_message::*}
        return {_message::*}

    if {_type} = "join_success":
        add "&2&lTEAM JOINED" to {_message::*}
        add "&aYou joined &f%{_value}%&a!" to {_message::*}
        return {_message::*}

    if {_type} = "join_notify_team":
        add "&2&lNEW MEMBER" to {_message::*}
        add "&a%{_value}%&a joined the team!" to {_message::*}
        return {_message::*}

    if {_type} = "leave_success":
        add "&6&lTEAM LEFT" to {_message::*}
        add "&eYou left &f%{_value}%&e." to {_message::*}
        return {_message::*}

    if {_type} = "leave_notify_team":
        add "&6&lMEMBER LEFT" to {_message::*}
        add "&e%{_value}%&e left the team." to {_message::*}
        return {_message::*}

    if {_type} = "promote_success":
        add "&2&lPROMOTED" to {_message::*}
        add "&a%{_value}%&a was promoted to Moderator by &f%{_value2}%&a." to {_message::*}
        return {_message::*}

    if {_type} = "demote_success":
        add "&6&lDEMOTED" to {_message::*}
        add "&e%{_value}%&e was demoted to Member by &f%{_value2}%&e." to {_message::*}
        return {_message::*}

    # ============ ERROR MESSAGES ============

    if {_type} = "error_not_in_team":
        add "&4&lERROR" to {_message::*}
        add "&cYou are not in a team." to {_message::*}
        return {_message::*}

    if {_type} = "error_already_in_team":
        add "&4&lERROR" to {_message::*}
        add "&cYou are already in a team." to {_message::*}
        return {_message::*}

    if {_type} = "error_name_taken":
        add "&4&lERROR" to {_message::*}
        add "&cTeam name already taken." to {_message::*}
        return {_message::*}

    if {_type} = "error_name_too_short":
        add "&4&lERROR" to {_message::*}
        add "&cName must be at least 4 characters." to {_message::*}
        return {_message::*}

    if {_type} = "error_name_too_long":
        add "&4&lERROR" to {_message::*}
        add "&cName must be under 8 characters." to {_message::*}
        return {_message::*}

    if {_type} = "error_not_owner":
        add "&4&lERROR" to {_message::*}
        add "&cOnly the owner can do this." to {_message::*}
        return {_message::*}

    if {_type} = "error_target_not_in_team":
        add "&4&lERROR" to {_message::*}
        add "&c%{_value}% is not in your team." to {_message::*}
        return {_message::*}

    if {_type} = "error_need_moderator":
        add "&4&lERROR" to {_message::*}
        add "&cYou need Moderator rank to %{_value}%." to {_message::*}
        return {_message::*}

    if {_type} = "error_cant_kick_higher":
        add "&4&lERROR" to {_message::*}
        add "&cCan't kick someone with equal or higher rank." to {_message::*}
        return {_message::*}

    if {_type} = "error_target_not_online":
        add "&4&lERROR" to {_message::*}
        add "&c%{_value}% is not online." to {_message::*}
        return {_message::*}

    if {_type} = "error_target_already_in_team":
        add "&4&lERROR" to {_message::*}
        add "&c%{_value}% is already in a team." to {_message::*}
        return {_message::*}

    if {_type} = "error_team_full":
        add "&4&lERROR" to {_message::*}
        add "&cTeam &f%{_value}%&c is full." to {_message::*}
        return {_message::*}

    if {_type} = "error_already_invited":
        add "&4&lERROR" to {_message::*}
        add "&c%{_value}% already has a pending invite." to {_message::*}
        return {_message::*}

    if {_type} = "error_no_invite":
        add "&4&lERROR" to {_message::*}
        add "&cNo pending invite from this team." to {_message::*}
        return {_message::*}

    if {_type} = "error_already_in_this_team":
        add "&4&lERROR" to {_message::*}
        add "&cYou are already in this team." to {_message::*}
        return {_message::*}

    if {_type} = "error_leave_in_other_team":
        add "&4&lERROR" to {_message::*}
        add "&cLeave your current team first." to {_message::*}
        return {_message::*}

    if {_type} = "error_owner_cant_leave":
        add "&4&lERROR" to {_message::*}
        add "&cOwners must /team disband or transfer ownership." to {_message::*}
        return {_message::*}

    if {_type} = "error_target_no_team":
        add "&4&lERROR" to {_message::*}
        add "&c%{_value}% is not in any team." to {_message::*}
        return {_message::*}

    if {_type} = "error_only_owner":
        add "&4&lERROR" to {_message::*}
        add "&cOnly the owner can %{_value}% members." to {_message::*}
        return {_message::*}

    if {_type} = "error_cant_promote_owner":
        add "&4&lERROR" to {_message::*}
        add "&cCan't promote the owner." to {_message::*}
        return {_message::*}

    if {_type} = "error_already_moderator":
        add "&4&lERROR" to {_message::*}
        add "&c%{_value}% is already a Moderator." to {_message::*}
        return {_message::*}

    if {_type} = "error_cant_demote_owner":
        add "&4&lERROR" to {_message::*}
        add "&cCan't demote yourself as owner." to {_message::*}
        return {_message::*}

    if {_type} = "error_already_member":
        add "&4&lERROR" to {_message::*}
        add "&c%{_value}% is already a Member." to {_message::*}
        return {_message::*}

    # Fallback
    add "&4&lERROR" to {_message::*}
    add "&cUnknown error." to {_message::*}
    return {_message::*}


function team_getPlayerTeamID(p: player) :: integer:
    return ({data::team::%{_p}'s uuid%} ? 0)

function team_getTeamName(id: integer) :: string:
    return {dataTeams::%{_id}%::name}

function team_getTeamOwner(id: integer) :: uuid:
    return {dataTeams::%{_id}%::owner}

function team_checkIfNameIsOK(name: string) :: string:
    if team_getAllTeamsNames() contains {_name}:
        return "error_name_taken"
    if length of {_name} < 4:
        return "error_name_too_short"
    if length of {_name} > 8:
        return "error_name_too_long"
    return "OK"

function team_create(p: player, name: string):
    if team_getPlayerTeamID({_p}) != 0:
        send team_getMessage("error_already_in_team") to {_p}
        stop
    
    set {_name} to lowercase {_name}

    set {_nameCheck} to team_checkIfNameIsOK({_name})
    if {_nameCheck} != "OK":
        send team_getMessage({_nameCheck}) to {_p}
        stop

    set {_id} to rng(1000000, 9999999)
    set {_teams.ids::*} to team_getAllTeamsIDs()

    while {_teams.ids::*} contains {_id}:
        set {_id} to rng(1000000, 9999999)
        wait 1 tick

    set {dataTeams::%{_id}%::name} to {_name}
    add {_name} to {data.teams::names::*}
    add {_id} to {data.teams::ids::*}

    set {data.teamByName::%{_name}%} to {_id}
    set {dataTeams::%{_id}%::owner} to {_p}'s uuid
    set {dataTeams::%{_id}%::members::*} to {_p}'s uuid

    set {data::team::%{_p}'s uuid%} to {_id}

    send team_getMessage("team_create", {_name}) to {_p}

function team_disband(p: player):
    set {_id} to team_getPlayerTeamID({_p})
    set {_name} to team_getTeamName({_id})

    if team_getTeamOwner({_id}) != {_p}'s uuid:
        send team_getMessage("error_not_owner") to {_p}
        stop
    
    set metadata tag "team_disband_confirm" of {_p} to true
    send team_getMessage("team_disband_confirm", {_name}) to {_p}

function team_delete(p: player, id: integer):
    set {_name} to team_getTeamName({_id})

    loop team_getAllMembers({_id}):
        delete {data::team::%loop-value%}
        set {_member} to loop-value parsed as a offline player
        if {_member} is online:
            if loop-value != {_p}'s uuid:
                send team_getMessage("team_disband_message_to_members", {_name}) to {_member}

    send team_getMessage("team_disband_message_to_owner", {_name}) to {_p}

    remove {_name} from {data.teams::names::*}
    remove {_id} from {data.teams::ids::*}

    delete {data.teamByName::%{_name}%}
    delete {dataTeams::%{_id}%::*}
    delete {dataTeams::%{_id}%}

function team_getPlayerRank(p: offlineplayer) :: string:
    set {_id} to team_getPlayerTeamID({_p})
    if {dataTeams::%{_id}%::owner} = {_p}'s uuid:
        return "Owner"
    if {dataTeams::%{_id}%::moderators::*} contains {_p}'s uuid:
        return "Moderator"
    return "Member"

function team_getRankWeight(rank: string) :: integer:
    if {_rank} = "Owner":
        return 5
    if {_rank} = "Moderator":
        return 3
    return 1

function team_canTargetBeInvited(p: player) :: boolean:
    if team_getPlayerTeamID({_p}) = 0:
        return true
    return false

function team_getUpgrade_maxMembers(id: integer) :: integer:
    return (4 + ({dataTeams::%{_id}%::upgrades::maxMembers} ? 0))

function team_isFull(id: integer) :: boolean:
    if size of team_getAllMembers({_id}) < team_getUpgrade_maxMembers({_id}):
        return false
    return true

function team_getAllMembers(id: integer) :: uuids:
    return {dataTeams::%{_id}%::members::*}

function team_getAllMembersAsNames(id: integer) :: players:
    loop team_getAllMembers({_id}):
        add loop-value parsed as a offlineplayer to {_members::*}
    return {_members::*}

function team_kick(p: player, target: offlineplayer):
    set {_id} to team_getPlayerTeamID({_p})
    if {_id} = 0:
        send team_getMessage("error_not_in_team") to {_p}
        stop

    if team_getAllMembers({_id}) doesn't contain uuid of {_target}:
        send team_getMessage("error_target_not_in_team", "%{_target}%") to {_p}
        stop

    set {_name} to team_getTeamName({_id})

    set {_rank} to team_getPlayerRank({_p})
    set {_rankWeight.player} to team_getRankWeight({_rank})
    if {_rankWeight.player} < 3:
        send team_getMessage("error_need_moderator", "kick") to {_p}
        stop

    set {_rankWeight.target} to team_getRankWeight(team_getPlayerRank({_target}))
    if {_rankWeight.target} >= {_rankWeight.player}:
        send team_getMessage("error_cant_kick_higher") to {_p}
        stop
    
    delete {data::team::%{_target}'s uuid%}
    remove {_target}'s uuid from {dataTeams::%{_id}%::members::*}
    if {_rankWeight.target} = 3:
        remove {_target}'s uuid from {dataTeams::%{_id}%::moderators::*}

    loop team_getAllMembers({_id}):
        set {_existingMember} to (loop-value parsed as a offlineplayer)
        if {_existingMember} is online:
            send team_getMessage("kick_success_team", "%{_target}%", "%{_p}%") to {_existingMember}

    send team_getMessage("kick_success_target", {_name}) to {_target}

function team_invite(p: player, target: offlineplayer):
    set {_id} to team_getPlayerTeamID({_p})
    if {_id} = 0:
        send team_getMessage("error_not_in_team") to {_p}
        stop

    set {_name} to team_getTeamName({_id})

    set {_rank} to team_getPlayerRank({_p})
    if team_getRankWeight({_rank}) < 3:
        send team_getMessage("error_need_moderator", "invite") to {_p}
        stop

    if {_target} is not online:
        send team_getMessage("error_target_not_online", "%{_target}%") to {_p}
        stop

    if team_canTargetBeInvited({_target}) = false:
        send team_getMessage("error_target_already_in_team", "%{_target}%") to {_p}
        stop
    
    if team_isFull({_id}) = true:
        send team_getMessage("error_team_full", {_name}) to {_p}
        stop
    
    if metadata tag "team_invited_%{_id}%" of {_target} is set:
        send team_getMessage("error_already_invited", "%{_target}%") to {_p}
        stop

    send team_getMessage("invite_sent", "%{_target}%", {_name}) to {_p}
    send team_getMessage("invite_received", {_name}, "%{_p}%") to {_target}

    set metadata tag "team_invited_%{_id}%" of {_target} to true
    wait 60 seconds
    delete metadata tag "team_invited_%{_id}%" of {_target}
    if team_getPlayerTeamID({_target}) != {_id}:
        send team_getMessage("invite_expired", {_name}) to {_target}

function team_join(p: player, id: integer):
    set {_name} to team_getTeamName({_id})

    set {_isInvited} to metadata tag "team_invited_%{_id}%" of {_p}
    if {_isInvited} is not set:
        send team_getMessage("error_no_invite") to {_p}
        stop

    set {_playerTeamID} to team_getPlayerTeamID({_p})
    if {_playerTeamID} = {_id}:
        send team_getMessage("error_already_in_this_team") to {_p}
        stop

    if {_playerTeamID} != 0:
        send team_getMessage("error_leave_in_other_team") to {_p}
        stop

    if team_isFull({_id}) = true:
        send team_getMessage("error_team_full", {_name}) to {_p}
        stop
    
    set {data::team::%{_p}'s uuid%} to {_id}
    loop team_getAllMembers({_id}):
        set {_existingMember} to (loop-value parsed as a offlineplayer)
        if {_existingMember} is online:
            send team_getMessage("join_notify_team", "%{_p}%") to {_existingMember}

    add {_p}'s uuid to {dataTeams::%{_id}%::members::*}
    delete metadata tag "team_invited_%{_id}%" of {_p}
    send team_getMessage("join_success", {_name}) to {_p}

function team_leave(p: player):
    set {_id} to team_getPlayerTeamID({_p})
    if {_id} = 0:
        send team_getMessage("error_not_in_team") to {_p}
        stop

    set {_name} to team_getTeamName({_id})

    if team_getTeamOwner({_id}) = {_p}'s uuid:
        send team_getMessage("error_owner_cant_leave") to {_p}
        stop

    set {_rank} to team_getPlayerRank({_p})
    if {_rank} = "Moderator":
        remove {_p}'s uuid from {dataTeams::%{_id}%::moderators::*}

    remove {_p}'s uuid from {dataTeams::%{_id}%::members::*}
    delete {data::team::%{_p}'s uuid%}

    loop team_getAllMembers({_id}):
        set {_member} to (loop-value parsed as a offlineplayer)
        if {_member} is online:
            send team_getMessage("leave_notify_team", "%{_p}%") to {_member}

    send team_getMessage("leave_success", {_name}) to {_p}

function team_getInfoMembersMessage(id: integer) :: strings:
    set {_owner} to team_getTeamOwner({_id})

    loop team_getAllMembers({_id}):
        if {_owner} = loop-value:
            continue

        set {_player} to loop-value parsed as offline player
        set {_rank} to team_getPlayerRank({_player})
        
        if {_player} is online:
            add "&8• &f%{_player}% &7(%{_rank}%) &aonline" to {_members::*}
            continue

        add "&8• &f%{_player}% &7(%{_rank}%) &coffline" to {_members::*}

    return {_members::*}

function team_info(p: player, target: offline player):
    set {_id} to team_getPlayerTeamID({_target})
    if {_id} = 0:
        send team_getMessage("error_target_no_team", "%{_target}%") to {_p}
        stop

    set {_name} to team_getTeamName({_id})
    set {_owner} to team_getTeamOwner({_id}) parsed as offline player

    set {_ownerStatus} to "&coffline"
    if {_owner} is online:
        set {_ownerStatus} to "&aonline"

    set {_members::*} to team_getInfoMembersMessage({_id})
    set {_memberCount} to size of team_getAllMembers({_id})
    set {_maxMembers} to team_getUpgrade_maxMembers({_id})

    send "" to {_p}
    send "&6&l%{_name}% &7(%{_memberCount}%/%{_maxMembers}%)" to {_p}
    send "" to {_p}
    send "&eOwner: &f%{_owner}% %{_ownerStatus}%" to {_p}
    send "" to {_p}
    if size of {_members::*} > 0:
        send "&eMembers:" to {_p}
        send {_members::*} to {_p}
    else:
        send "&7No other members." to {_p}
    send "" to {_p}

function team_getIDByName(name: string) :: integer:
    return {data.teamByName::%{_name}%}

function team_promote(p: player, target: offlineplayer):
    set {_id} to team_getPlayerTeamID({_p})
    if {_id} = 0:
        send team_getMessage("error_not_in_team") to {_p}
        stop

    if team_getAllMembers({_id}) doesn't contain uuid of {_target}:
        send team_getMessage("error_target_not_in_team", "%{_target}%") to {_p}
        stop

    if team_getTeamOwner({_id}) != {_p}'s uuid:
        send team_getMessage("error_only_owner", "promote") to {_p}
        stop

    set {_targetRank} to team_getPlayerRank({_target})
    if {_targetRank} = "Owner":
        send team_getMessage("error_cant_promote_owner") to {_p}
        stop
    if {_targetRank} = "Moderator":
        send team_getMessage("error_already_moderator", "%{_target}%") to {_p}
        stop

    add {_target}'s uuid to {dataTeams::%{_id}%::moderators::*}

    loop team_getAllMembers({_id}):
        set {_member} to (loop-value parsed as a offlineplayer)
        if {_member} is online:
            send team_getMessage("promote_success", "%{_target}%", "%{_p}%") to {_member}

function team_demote(p: player, target: offlineplayer):
    set {_id} to team_getPlayerTeamID({_p})
    if {_id} = 0:
        send team_getMessage("error_not_in_team") to {_p}
        stop

    if team_getAllMembers({_id}) doesn't contain uuid of {_target}:
        send team_getMessage("error_target_not_in_team", "%{_target}%") to {_p}
        stop

    if team_getTeamOwner({_id}) != {_p}'s uuid:
        send team_getMessage("error_only_owner", "demote") to {_p}
        stop

    set {_targetRank} to team_getPlayerRank({_target})
    if {_targetRank} = "Owner":
        send team_getMessage("error_cant_demote_owner") to {_p}
        stop
    if {_targetRank} = "Member":
        send team_getMessage("error_already_member", "%{_target}%") to {_p}
        stop

    remove {_target}'s uuid from {dataTeams::%{_id}%::moderators::*}

    loop team_getAllMembers({_id}):
        set {_member} to (loop-value parsed as a offlineplayer)
        if {_member} is online:
            send team_getMessage("demote_success", "%{_target}%", "%{_p}%") to {_member}


on chat:
    set {_message} to message
    if metadata tag "team_disband_confirm" of player = true:
        cancel event
        set {_id} to team_getPlayerTeamID(player)
        set {_name} to team_getTeamName({_id})
        if {_message} = "I confirm deletion of team %{_name}%":
            delete metadata tag "team_disband_confirm" of player
            team_delete(player, {_id})
            stop
        delete metadata tag "team_disband_confirm" of player
        send team_getMessage("team_disband_cancelled") to player


command /delteams:
    permission: op
    trigger:
        delete {dataTeams::*}
        delete {data.teams::names::*}
        delete {data.teams::ids::*}
        delete {data::team::*}


on tab complete of "/team":
    set {_id} to team_getPlayerTeamID(player)
    set tab completions for position 1 to "help", "create", "disband", "promote", "demote", "invite", "join", "leave", "kick", "info" and "upgrades"
    set {_arg.1} to tab arg-1
    if {_arg.1} = "create":
        set tab completions for position 2 to "<team name>"
        stop
    if {_arg.1} = "invite":
        set tab completions for position 2 to all players
        stop
    if {_arg.1} = "kick":
        set tab completions for position 2 to team_getAllMembersAsNames({_id})
        stop
    if {_arg.1} = "info":
        set tab completions for position 2 to all players
        stop
    if {_arg.1} = "promote":
        set tab completions for position 2 to team_getAllMembersAsNames({_id})
        stop
    if {_arg.1} = "demote":
        set tab completions for position 2 to team_getAllMembersAsNames({_id})
        stop

command /team [<string>] [<string>]:
    trigger:
        if arg-1 is not set:
            send team_getMessage("help") to player
            stop
        if arg-1 = "help":
            send team_getMessage("help") to player
            stop
        if arg-1 = "create":
            if arg-2 is not set:
                send "&cUsage: /team create <name>"
                stop
            team_create(player, arg-2)
            stop
        if arg-1 = "disband":
            if arg-2 is set:
                send "&cUsage: /team disband"
                stop
            team_disband(player)
            stop
        if arg-1 = "invite":
            if arg-2 is not set:
                send "&cUsage: /team invite <player>"
                stop
            set {_target} to arg-2 parsed as offline player
            team_invite(player, {_target})
            stop
        if arg-1 = "join":
            if arg-2 is not set:
                send "&cUsage: /team join <team_name>"
                stop
            set {_id} to team_getIDByName(arg-2)
            team_join(player, {_id})
            stop
        if arg-1 = "kick":
            if arg-2 is not set:
                send "&cUsage: /team kick <player>"
                stop
            set {_target} to arg-2 parsed as offline player
            team_kick(player, {_target})
            stop
        if arg-1 = "info":
            if arg-2 is not set:
                team_info(player, player)
                stop
            set {_target} to arg-2 parsed as offline player
            team_info(player, {_target})
            stop
        if arg-1 = "promote":
            if arg-2 is not set:
                send "&cUsage: /team promote <player>"
                stop
            set {_target} to arg-2 parsed as offline player
            team_promote(player, {_target})
            stop
        if arg-1 = "demote":
            if arg-2 is not set:
                send "&cUsage: /team demote <player>"
                stop
            set {_target} to arg-2 parsed as offline player
            team_demote(player, {_target})
            stop
        if arg-1 = "leave":
            if arg-2 is set:
                send "&cUsage: /team leave"
                stop
            team_leave(player)
            stop
