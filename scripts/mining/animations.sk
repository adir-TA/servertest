# ============================================================
# MINING ANIMATIONS
# ============================================================

# ============================================================
# BLOCK LOOT ANIMATION
# ============================================================

function packetBlockLoot(p: player, loc: location, block: item):
    # Check mining animations option
    if options_getBool({_p}, "mining_animations", true) is false:
        stop

    set {_baseId} to random integer between 100000 and 9999999
    
    set {_bx} to floor(x-coord of {_loc}) + 0.5
    set {_by} to floor(y-coord of {_loc})
    set {_bz} to floor(z-coord of {_loc}) + 0.5
    
    set {_center} to location({_bx}, {_by}, {_bz}, world of {_loc})
    set {_pos} to {_center} ~ vector(0, 1.3, 0)
    
    spawn fake item display at {_pos} for {_p} with id {_baseId}
    
    set {_m} to new metadata packet with id {_baseId}:
        display item: {_block}
        display scale: vector(0.01, 0.01, 0.01)
        display billboard: center
        display brightness block: 15
        display teleportduration: 2
        display transformation: 3
        display interpolation: 0
        glowing: true
        display glow: rgb(255, 255, 255)
    send packet {_m} to {_p}
    
    wait 1 tick
    
    set {_m} to new metadata packet with id {_baseId}:
        display scale: vector(0.4, 0.4, 0.4)
        display transformation: 4
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 3 ticks
    
    loop 16 times:
        set {_frame} to loop-number - 1
        
        set {_floatOffset} to sin({_frame} * 22.5) * 0.12
        set {_pos} to {_center} ~ vector(0, 1.3 + {_floatOffset}, 0)
        send teleport packet with id {_baseId} with location {_pos} to {_p}
        
        set {_angle} to {_frame} * 22.5
        set {_qy} to sin({_angle} / 2)
        set {_qw} to cos({_angle} / 2)
        
        set {_m} to new metadata packet with id {_baseId}:
            display rotation left: 0, {_qy}, 0, {_qw}
            display transformation: 2
            display interpolation: 0
        send packet {_m} to {_p}
        
        wait 2 ticks
    
    wait 2 ticks
    
    set {_pos} to {_center} ~ vector(0, 1.8, 0)
    send teleport packet with id {_baseId} with location {_pos} to {_p}
    
    set {_m} to new metadata packet with id {_baseId}:
        display scale: vector(0.2, 0.2, 0.2)
        display transformation: 5
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 5 ticks
    
    set {_m} to new metadata packet with id {_baseId}:
        display scale: vector(0.01, 0.01, 0.01)
        display transformation: 4
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 4 ticks
    
    remove fake entity with id {_baseId} for {_p}


# ============================================================
# PROSPECTING POUCH ANIMATION
# ============================================================

function packetPouchGems(p: player, loc: location, tier: integer, gemCount: integer, doubled: boolean):
    set {_baseId} to random integer between 100000 and 9999999
    set {_pouchId} to {_baseId}
    
    set {_gemItem} to gem_getItem({_tier})
    set {_glowColor} to gem_getGlowColor({_tier})
    set {_gemName} to gem_getName({_tier})
    set {_tierColor} to gem_getTierColor({_tier})
    
    set {_blockY} to floor(y-coord of {_loc})
    set {_surfaceY} to {_blockY} + 1
    set {_bx} to floor(x-coord of {_loc}) + 0.5
    set {_bz} to floor(z-coord of {_loc}) + 0.5
    set {_pouchY} to {_surfaceY} + 0.5
    set {_baseLoc} to location({_bx}, {_pouchY}, {_bz}, world of {_loc})
    set {_groundY} to {_surfaceY} + 0.08
    
    spawn fake item display at {_baseLoc} for {_p} with id {_pouchId}
    
    set {_m} to new metadata packet with id {_pouchId}:
        display item: bundle
        display scale: vector(0.01, 0.01, 0.01)
        display billboard: center
        display brightness block: 15
        display teleportduration: 2
        display transformation: 3
        display interpolation: 0
        glowing: true
        display glow: {_glowColor}
    send packet {_m} to {_p}
    
    play sound "entity.item.pickup" with volume 0.6 and pitch 0.8 to {_p}
    
    wait 1 tick
    
    set {_m} to new metadata packet with id {_pouchId}:
        display scale: vector(0.5, 0.5, 0.5)
        display transformation: 4
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 3 ticks
    
    set {_bounceUp} to location({_bx}, {_pouchY} + 0.3, {_bz}, world of {_loc})
    send teleport packet with id {_pouchId} with location {_bounceUp} to {_p}
    
    set {_m} to new metadata packet with id {_pouchId}:
        display scale: vector(0.55, 0.45, 0.55)
        display transformation: 3
        display interpolation: 0
    send packet {_m} to {_p}
    
    play sound "block.wool.hit" with volume 0.5 and pitch 1.2 to {_p}
    
    wait 3 ticks
    
    send teleport packet with id {_pouchId} with location {_baseLoc} to {_p}
    
    set {_m} to new metadata packet with id {_pouchId}:
        display scale: vector(0.6, 0.4, 0.6)
        display transformation: 3
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 2 ticks
    
    set {_bounceUp2} to location({_bx}, {_pouchY} + 0.15, {_bz}, world of {_loc})
    send teleport packet with id {_pouchId} with location {_bounceUp2} to {_p}
    
    set {_m} to new metadata packet with id {_pouchId}:
        display scale: vector(0.5, 0.5, 0.5)
        display transformation: 3
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 2 ticks
    
    send teleport packet with id {_pouchId} with location {_baseLoc} to {_p}
    
    wait 3 ticks
    
    set {_shakeLeft} to location({_bx} - 0.05, {_pouchY}, {_bz}, world of {_loc})
    send teleport packet with id {_pouchId} with location {_shakeLeft} to {_p}
    wait 1 tick
    
    set {_shakeRight} to location({_bx} + 0.05, {_pouchY}, {_bz}, world of {_loc})
    send teleport packet with id {_pouchId} with location {_shakeRight} to {_p}
    wait 1 tick
    
    send teleport packet with id {_pouchId} with location {_shakeLeft} to {_p}
    wait 1 tick
    
    send teleport packet with id {_pouchId} with location {_baseLoc} to {_p}
    
    play sound "block.amethyst_block.chime" with volume 0.8 and pitch 1.2 to {_p}
    
    wait 2 ticks
    
    set {_m} to new metadata packet with id {_pouchId}:
        display scale: vector(0.8, 0.8, 0.8)
        display transformation: 2
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 2 ticks
    
    set {_index} to 0
    
    loop {_gemCount} times:
        set {_n} to loop-number
        add 1 to {_index}
        set {_id::%{_index}%} to {_baseId} + {_index}
        
        set {_angle} to ({_n} - 1) * (360 / {_gemCount}) + random number between -20 and 20
        set {_landDist} to random number between 0.4 and 0.8
        set {_peakHeight} to random number between 0.5 and 0.9
        
        set {_angleOf::%{_index}%} to {_angle}
        set {_landDistOf::%{_index}%} to {_landDist}
        set {_peakHeightOf::%{_index}%} to {_peakHeight}
        
        set {_s} to random number between 0.15 and 0.28
        set {_scaleOf::%{_index}%} to {_s}
        
        set {_bounceHeight::%{_index}%} to random number between 0.1 and 0.2
        set {_rotDir::%{_index}%} to random number between -0.7 and 0.7
        
        spawn fake item display at {_baseLoc} for {_p} with id {_id::%{_index}%}
        
        set {_m} to new metadata packet with id {_id::%{_index}%}:
            display item: {_gemItem}
            display scale: vector(0.01, 0.01, 0.01)
            display billboard: center
            display brightness block: 15
            display teleportduration: 1
            display interpolation: 0
            display transformation: 0
            glowing: true
            display glow: {_glowColor}
        send packet {_m} to {_p}
    
    set {_total} to {_index}
    
    set {_m} to new metadata packet with id {_pouchId}:
        display scale: vector(0.01, 0.01, 0.01)
        display transformation: 2
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 1 tick
    
    remove fake entity with id {_pouchId} for {_p}
    
    loop {_total} times:
        set {_i} to loop-number
        set {_angle} to {_angleOf::%{_i}%}
        set {_landDist} to {_landDistOf::%{_i}%}
        set {_peakHeight} to {_peakHeightOf::%{_i}%}
        set {_s} to {_scaleOf::%{_i}%}
        set {_rot} to {_rotDir::%{_i}%}
        
        set {_x} to ({_landDist} * 0.5) * cos({_angle})
        set {_z} to ({_landDist} * 0.5) * sin({_angle})
        set {_peakY} to {_pouchY} + {_peakHeight}
        set {_peakPos} to location({_bx} + {_x}, {_peakY}, {_bz} + {_z}, world of {_loc})
        
        send teleport packet with id {_id::%{_i}%} with location {_peakPos} to {_p}
        
        set {_m} to new metadata packet with id {_id::%{_i}%}:
            display scale: vector({_s}, {_s}, {_s})
            display teleportduration: 6
            display transformation: 4
            display interpolation: 0
            display rotation left: 0, {_rot}, 0, 0.7
        send packet {_m} to {_p}
    
    play sound "block.amethyst_cluster.break" with volume 0.7 and pitch 1.5 to {_p}
    
    wait 6 ticks
    
    loop {_total} times:
        set {_i} to loop-number
        set {_angle} to {_angleOf::%{_i}%}
        set {_landDist} to {_landDistOf::%{_i}%}
        set {_rot} to {_rotDir::%{_i}%}
        
        set {_x} to {_landDist} * cos({_angle})
        set {_z} to {_landDist} * sin({_angle})
        
        set {_groundPos} to location({_bx} + {_x}, {_groundY}, {_bz} + {_z}, world of {_loc})
        send teleport packet with id {_id::%{_i}%} with location {_groundPos} to {_p}
        
        set {_m} to new metadata packet with id {_id::%{_i}%}:
            display teleportduration: 8
            display interpolation: 0
            display transformation: 6
            display rotation left: 0, {_rot}, 0, 0.4
        send packet {_m} to {_p}
    
    wait 8 ticks
    
    loop {_total} times:
        set {_i} to loop-number
        set {_angle} to {_angleOf::%{_i}%}
        set {_landDist} to {_landDistOf::%{_i}%}
        set {_bh} to {_bounceHeight::%{_i}%}
        set {_rot} to {_rotDir::%{_i}%}
        
        set {_x} to {_landDist} * cos({_angle})
        set {_z} to {_landDist} * sin({_angle})
        
        set {_bouncePos} to location({_bx} + {_x}, {_groundY} + {_bh}, {_bz} + {_z}, world of {_loc})
        send teleport packet with id {_id::%{_i}%} with location {_bouncePos} to {_p}
        
        set {_m} to new metadata packet with id {_id::%{_i}%}:
            display teleportduration: 4
            display interpolation: 0
            display transformation: 4
            display rotation left: 0, {_rot}, 0, 0.2
        send packet {_m} to {_p}
    
    wait 5 ticks
    
    loop {_total} times:
        set {_i} to loop-number
        set {_angle} to {_angleOf::%{_i}%}
        set {_landDist} to {_landDistOf::%{_i}%}
        
        set {_x} to {_landDist} * cos({_angle})
        set {_z} to {_landDist} * sin({_angle})
        
        set {_settlePos} to location({_bx} + {_x}, {_groundY}, {_bz} + {_z}, world of {_loc})
        send teleport packet with id {_id::%{_i}%} with location {_settlePos} to {_p}
        
        set {_m} to new metadata packet with id {_id::%{_i}%}:
            display teleportduration: 5
            display interpolation: 0
            display transformation: 5
            display rotation left: 0, 0, 0, 1
        send packet {_m} to {_p}
    
    wait 12 ticks
    
    loop {_total} times:
        set {_i} to loop-number
        set {_rot} to {_rotDir::%{_i}%}
        
        set {_playerLoc} to location of {_p}
        set {_playerLoc} to {_playerLoc} ~ vector(0, 1.2, 0)
        
        set {_m} to new metadata packet with id {_id::%{_i}%}:
            display teleportduration: 3
            display interpolation: 0
            display transformation: 3
            display rotation left: 0, {_rot}, 0, 0.3
        send packet {_m} to {_p}
        
        send teleport packet with id {_id::%{_i}%} with location {_playerLoc} to {_p}
        
        wait 3 ticks
        
        set {_pitch} to random number between 1.0 and 1.5
        play sound "entity.item.pickup" with volume 0.5 and pitch {_pitch} to {_p}
        
        remove fake entity with id {_id::%{_i}%} for {_p}
    
    prospecting_showText({_p}, {_loc}, {_tier}, {_gemCount}, {_doubled})


function prospecting_showText(p: player, loc: location, tier: integer, amount: integer, doubled: boolean):
    set {_textId} to random integer between 100000 and 9999999
    
    set {_gemName} to gem_getName({_tier})
    set {_tierColor} to gem_getTierColor({_tier})
    
    set {_bx} to floor(x-coord of {_loc}) + 0.5
    set {_by} to floor(y-coord of {_loc}) + 2.5
    set {_bz} to floor(z-coord of {_loc}) + 0.5
    set {_pos} to location({_bx}, {_by}, {_bz}, world of {_loc})
    
    if {_doubled} = true:
        set {_text} to "%{_tierColor}%&l+%{_amount}% %{_gemName}% &d&lDOUBLED!"
        play sound "entity.player.levelup" with volume 0.6 and pitch 1.5 to {_p}
    else:
        set {_text} to "%{_tierColor}%&l+%{_amount}% %{_gemName}%"
    
    spawn fake text display at {_pos} for {_p} with id {_textId}
    
    set {_m} to new metadata packet with id {_textId}:
        display text: {_text}
        display billboard: center
        display brightness block: 15
        display shadow: true
        display defaultbackground: false
        display scale: vector(0.01, 0.01, 0.01)
        display teleportduration: 2
    send packet {_m} to {_p}
    
    wait 1 tick
    
    set {_m} to new metadata packet with id {_textId}:
        display scale: vector(1, 1, 1)
        display transformation: 2
        display interpolation: 0
    send packet {_m} to {_p}
    
    set {_endPos} to location({_bx}, {_by} + 0.5, {_bz}, world of {_loc})
    send teleport packet with id {_textId} with location {_endPos} to {_p}
    
    wait 25 ticks
    
    set {_m} to new metadata packet with id {_textId}:
        display scale: vector(0.01, 0.01, 0.01)
        display transformation: 3
        display interpolation: 0
    send packet {_m} to {_p}
    
    wait 4 ticks
    
    remove fake entity with id {_textId} for {_p}


