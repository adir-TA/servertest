# ============================================================
# ECONOMY SYSTEM
# ============================================================
#
# Variables:
#   {data::balance::%uuid%}
#   {data::tokens::%uuid%}
#   {data::prestige::%uuid|}
#   {data::prestige::points::%uuid%}
#
# ============================================================

# ============================================================
# TOKENS
# ============================================================

function economy_getTokens(p: player) :: number:
    return {data::tokens::%{_p}'s uuid%} ? 0

function economy_setTokens(p: player, amount: number):
    set {data::tokens::%{_p}'s uuid%} to {_amount}

function economy_addTokens(p: player, amount: number):
    add {_amount} to {data::tokens::%{_p}'s uuid%}

function economy_removeTokens(p: player, amount: number) :: boolean:
    set {_current} to economy_getTokens({_p})
    if {_current} < {_amount}:
        return false
    subtract {_amount} from {data::tokens::%{_p}'s uuid%}
    return true

function economy_hasTokens(p: player, amount: number) :: boolean:
    if economy_getTokens({_p}) >= {_amount}:
        return true
    return false

# ============================================================
# MONEY
# ============================================================

function economy_getMoney(p: player) :: number:
    return {data::balance::%{_p}'s uuid%} ? 0

function economy_setMoney(p: player, amount: number):
    set {data::balance::%{_p}'s uuid%} to {_amount}

function economy_addMoney(p: player, amount: number):
    add {_amount} to {data::balance::%{_p}'s uuid%}

function economy_removeMoney(p: player, amount: number) :: boolean:
    if {data::balance::%{_p}'s uuid%} < {_amount}:
        return false
    subtract {_amount} from {data::balance::%{_p}'s uuid%}
    return true

function economy_hasMoney(p: player, amount: number) :: boolean:
    if economy_getMoney({_p}) >= {_amount}:
        return true
    return false

# ============================================================
# PRESTIGE POINTS
# ============================================================

function economy_getPrestigePoints(p: player) :: number:
    return {data::prestige::points::%{_p}'s uuid%} ? 0

function economy_setPrestigePoints(p: player, amount: number):
    set {data::prestige::points::%{_p}'s uuid%} to {_amount}

function economy_addPrestigePoints(p: player, amount: number):
    add {_amount} to {data::prestige::points::%{_p}'s uuid%}

function economy_removePrestigePoints(p: player, amount: number) :: boolean:
    set {_current} to economy_getPrestigePoints({_p})
    if {_current} < {_amount}:
        return false
    subtract {_amount} from {data::prestige::points::%{_p}'s uuid%}
    return true

function economy_hasPrestigePoints(p: player, amount: number) :: boolean:
    if economy_getPrestigePoints({_p}) >= {_amount}:
        return true
    return false

# ============================================================
# TOTAL PRESTIGES
# ============================================================

function economy_getTotalPrestiges(p: player) :: number:
    set {_uuid} to {_p}'s uuid
    set {_total} to 0
    # Sum only the 3 main archetypes (exclude Global/tutorial)
    add ({mining::precision::%{_uuid}%::prestige} ? 0) to {_total}
    add ({mining::demolition::%{_uuid}%::prestige} ? 0) to {_total}
    add ({mining::arcane::%{_uuid}%::prestige} ? 0) to {_total}
    return {_total}

function economy_addTotalPrestige(p: player):
    add 1 to {data::prestige::%{_p}'s uuid%}

# ============================================================
# FORMATTED DISPLAYS
# ============================================================

function economy_getFormattedMoney(p: player) :: text:
    set {_bal} to economy_getMoney({_p})
    return "&2$&a%formatNum({_bal})%"

function economy_getFormattedTokens(p: player) :: text:
    set {_tokens} to economy_getTokens({_p})
    return "&6⛂&e%formatNum({_tokens})%"

function economy_getFormattedPrestigePoints(p: player) :: text:
    set {_points} to economy_getPrestigePoints({_p})
    return "&d✦&5%formatNum({_points})%"


# ============================================================
# ADMIN COMMAND
# ============================================================
# /eco give <currency> <player> <amount>
# /eco take <currency> <player> <amount>
# /eco set <currency> <player> <amount>
# /eco check <currency> <player>
#
# Currencies: money, tokens, prestige
# ============================================================

command /eco [<text>] [<text>] [<text>] [<text>]:
    permission: op
    trigger:
        set {_action} to arg-1
        set {_currency} to arg-2
        set {_targetName} to arg-3
        set {_amount} to arg-4 parsed as number

        # Usage check
        if {_action} is not set:
            send ""
            send "&6&lEconomy Admin"
            send "&7/eco give &e<currency> <player> <amount>"
            send "&7/eco take &e<currency> <player> <amount>"
            send "&7/eco set &e<currency> <player> <amount>"
            send "&7/eco check &e<currency> <player>"
            send ""
            send "&7Currencies: &amoney&7, &etokens&7, &dprestige"
            send ""
            stop

        # Validate currency
        if {_currency} is not "money", "tokens" or "prestige":
            send "&cInvalid currency. Use: &7money, tokens, prestige"
            stop

        # Validate target
        if {_targetName} is not set:
            send "&cUsage: /eco %{_action}% %{_currency}% <player> [amount]"
            stop

        set {_target} to {_targetName} parsed as player
        if {_target} is not set:
            send "&cPlayer &7%{_targetName}% &cis not online."
            stop

        # Check command
        if {_action} is "check":
            if {_currency} is "money":
                set {_val} to economy_getMoney({_target})
                send "&7%{_target}%'s money: &a$%formatNum({_val})%"
            else if {_currency} is "tokens":
                set {_val} to economy_getTokens({_target})
                send "&7%{_target}%'s tokens: &e%formatNum({_val})%"
            else if {_currency} is "prestige":
                set {_val} to economy_getPrestigePoints({_target})
                send "&7%{_target}%'s prestige points: &d%formatNum({_val})%"
            stop

        # Validate amount for give/take/set
        if {_amount} is not set:
            send "&cUsage: /eco %{_action}% %{_currency}% <player> <amount>"
            stop
        if {_amount} < 0:
            send "&cAmount must be positive."
            stop

        # Give
        if {_action} is "give":
            if {_currency} is "money":
                economy_addMoney({_target}, {_amount})
                set {_val} to economy_getMoney({_target})
                send "&aGave &2$%formatNum({_amount})% &ato &7%{_target}%&a. New balance: &2$%formatNum({_val})%"
            else if {_currency} is "tokens":
                economy_addTokens({_target}, {_amount})
                set {_val} to economy_getTokens({_target})
                send "&aGave &e%formatNum({_amount})% tokens &ato &7%{_target}%&a. New balance: &e%formatNum({_val})%"
            else if {_currency} is "prestige":
                economy_addPrestigePoints({_target}, {_amount})
                set {_val} to economy_getPrestigePoints({_target})
                send "&aGave &d%formatNum({_amount})% prestige pts &ato &7%{_target}%&a. New balance: &d%formatNum({_val})%"
            stop

        # Take
        if {_action} is "take":
            if {_currency} is "money":
                set {_val} to economy_getMoney({_target})
                if {_val} < {_amount}:
                    send "&cPlayer only has &2$%formatNum({_val})%&c."
                    stop
                economy_removeMoney({_target}, {_amount})
                set {_val} to economy_getMoney({_target})
                send "&cTook &2$%formatNum({_amount})% &cfrom &7%{_target}%&c. New balance: &2$%formatNum({_val})%"
            else if {_currency} is "tokens":
                set {_val} to economy_getTokens({_target})
                if {_val} < {_amount}:
                    send "&cPlayer only has &e%formatNum({_val})% tokens&c."
                    stop
                economy_removeTokens({_target}, {_amount})
                set {_val} to economy_getTokens({_target})
                send "&cTook &e%formatNum({_amount})% tokens &cfrom &7%{_target}%&c. New balance: &e%formatNum({_val})%"
            else if {_currency} is "prestige":
                set {_val} to economy_getPrestigePoints({_target})
                if {_val} < {_amount}:
                    send "&cPlayer only has &d%formatNum({_val})% pts&c."
                    stop
                economy_removePrestigePoints({_target}, {_amount})
                set {_val} to economy_getPrestigePoints({_target})
                send "&cTook &d%formatNum({_amount})% prestige pts &cfrom &7%{_target}%&c. New balance: &d%formatNum({_val})%"
            stop

        # Set
        if {_action} is "set":
            if {_currency} is "money":
                economy_setMoney({_target}, {_amount})
                send "&aSet &7%{_target}%&a's money to &2$%formatNum({_amount})%"
            else if {_currency} is "tokens":
                economy_setTokens({_target}, {_amount})
                send "&aSet &7%{_target}%&a's tokens to &e%formatNum({_amount})%"
            else if {_currency} is "prestige":
                economy_setPrestigePoints({_target}, {_amount})
                send "&aSet &7%{_target}%&a's prestige points to &d%formatNum({_amount})%"
            stop

        send "&cUnknown action. Use: &7give, take, set, check"